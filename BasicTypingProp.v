From stdpp Require Import mapset.
From CT Require Import CoreLangProp.
From CT Require Import BasicTyping.
From CT Require Import OperationalSemantics.

Import Atom.
Import CoreLang.
Import Tactics.
Import NamelessTactics.
Import BasicTyping.
Import OperationalSemantics.

(** * Naming related properties of the basic type system *)

Lemma basic_typing_contains_fv_tm: forall Γ e T, Γ ⊢t e ⋮t T -> fv_tm e ⊆ dom Γ
with basic_typing_contains_fv_value: forall Γ e T, Γ ⊢t e ⋮v T -> fv_value e ⊆ dom Γ.
Proof.
  all:
  destruct 1; simpl; eauto;
  try select (forall x, _ ∉ _ -> _) (fun H => auto_pose_fv x; repeat specialize_with x);
  repeat select (_ ⊢t _ ⋮v _) (fun H => apply basic_typing_contains_fv_value in H);
  repeat select (_ ⊢t _ ⋮t _) (fun H => apply basic_typing_contains_fv_tm in H);
  simpl in *;
  repeat
    match goal with
    | H : fv_tm ({_ ~t> _} _) ⊆ _ |- _ =>
        setoid_rewrite <- open_fv_tm' in H
    | H : _ ⊆ dom (<[_:=_]>_) |- _ =>
        setoid_rewrite dom_insert in H
    | H : _ !! _ = _ |- _ =>
        apply elem_of_dom_2 in H
    end;
  my_set_solver.
Qed.

Lemma basic_typing_closed_tm: forall e T, ∅ ⊢t e ⋮t T -> closed_tm e.
Proof.
  intros.
  apply basic_typing_contains_fv_tm in H. my_set_solver.
Qed.

Lemma basic_typing_closed_value: forall v T, ∅ ⊢t v ⋮v T -> closed_value v.
Proof.
  intros.
  apply basic_typing_contains_fv_value in H. my_set_solver.
Qed.

Lemma basic_typing_regular_value: forall Γ v t, Γ ⊢t v ⋮v t -> lc v
with basic_typing_regular_tm: forall Γ e t, Γ ⊢t e ⋮t t -> lc e.
Proof.
  all: destruct 1; simpl;
    try econstructor; try instantiate_atom_listctx; eauto.
Qed.

Ltac lc_basic_typing_simp_aux :=
  match goal with
  | [H: _ ⊢t ?e ⋮t _ |- lc ?e] => apply basic_typing_regular_tm in H
  | [H: _ ⊢t ?v ⋮v _ |- lc (treturn ?v)] => apply basic_typing_regular_value in H
  | [H: _ ⊢t _ ⋮v _ |- lc _] => apply basic_typing_regular_value in H; simp_hyps
  | [H: _ ⊢t _ ⋮t _ |- lc _] => apply basic_typing_regular_tm in H; simp_hyps
  | [H: _ ⊢t _ ⋮v _ |- body _] => apply basic_typing_regular_value in H; simp_hyps
  | [H: _ ⊢t _ ⋮t _ |- body _] => apply basic_typing_regular_tm in H; simp_hyps
  | [H: _ ⊢t _ ⋮v _ |- context [fv_value _]] =>
      apply basic_typing_closed_value in H; simp_hyps
  | [H: _ ⊢t _ ⋮t _ |- context [fv_value _]] =>
      apply basic_typing_closed_tm in H; simp_hyps
  | [H: _ ⊢t _ ⋮v _ |- context [fv_tm _]] =>
      apply basic_typing_closed_value in H; simp_hyps
  | [H: _ ⊢t _ ⋮t _ |- context [fv_tm _]] =>
      apply basic_typing_closed_tm in H; simp_hyps
  end; eauto.

Ltac basic_typing_regular_simp :=
  repeat lc_basic_typing_simp_aux; eauto.


(** This file proves properties of the basic type system. *)

(** * Canonical forms *)

Lemma basic_typing_bool_canonical_form: forall (v: value), ∅ ⊢t v ⋮v TBool -> v = true \/ v = false.
Proof.
  inversion 1; subst; simpl in *.
  destruct c; simplify_eq. destruct b; eauto.
  simplify_map_eq.
Qed.

Lemma basic_typing_nat_canonical_form: forall (v: value), ∅ ⊢t v ⋮v TNat -> (exists (i: nat), v = i).
Proof.
  inversion 1; subst; simpl in *.
  destruct c; simplify_eq. eauto.
  simplify_map_eq.
Qed.

Lemma basic_typing_base_canonical_form: forall (v: value) (B: base_ty), ∅ ⊢t v ⋮v B -> (exists (c: constant), v = c).
Proof.
  inversion 1; subst. eauto. simplify_map_eq.
Qed.

Lemma basic_typing_arrow_canonical_form:
  forall (v: value) T1 T2, ∅ ⊢t v ⋮v T1 ⤍ T2 ->
                      (exists e, v = vlam T1 e) \/ (exists e, v = vfix (T1 ⤍ T2) (vlam (T1 ⤍ T2) e)).
Proof.
  inversion 1; subst. simplify_map_eq.
  - left. eauto.
  - right. eexists. f_equal.
Qed.


(** * Weakening lemmas *)

Lemma basic_typing_weaken_tm: forall Γ Γ' (v: tm) T,
    Γ ⊆ Γ' -> Γ ⊢t v ⋮t T -> Γ' ⊢t v ⋮t T
with basic_typing_weaken_value: forall Γ Γ' (v: value) T,
    Γ ⊆ Γ' -> Γ ⊢t v ⋮v T -> Γ' ⊢t v ⋮v T.
Proof.
  all : intros * Hs; destruct 1; econstructor; eauto using lookup_weaken.
  all: instantiate_atom_listctx;
    auto_eapply;
    try lazymatch goal with
      | |- _ ⊆ _ => apply insert_mono; eauto
      | _ => eauto
      end.
Qed.

Lemma basic_typing_weaken_insert_tm: forall Γ (v: tm) T x U,
    x # Γ -> Γ ⊢t v ⋮t T -> <[x := U]>Γ ⊢t v ⋮t T.
Proof.
  intros. eapply basic_typing_weaken_tm. 2: eauto.
  apply insert_subseteq. apply not_elem_of_dom. my_set_solver.
Qed.

Lemma basic_typing_weaken_insert_value: forall Γ (v: value) T x U,
    x # Γ -> Γ ⊢t v ⋮v T -> <[x := U]> Γ ⊢t v ⋮v T.
Proof.
  intros. eapply basic_typing_weaken_value. 2: eauto.
  apply insert_subseteq. apply not_elem_of_dom. my_set_solver.
Qed.

(** * Substitution lemmas *)

Lemma basic_typing_subst_tm: forall Γ z u U (v: tm) T, Γ ⊢t u ⋮v U -> <[z := U]> Γ ⊢t v ⋮t T -> Γ ⊢t {z := u}t v ⋮t T.
Proof.
  intros * Hu Ht; remember (<[z:=U]> Γ) as Γ'; generalize dependent Γ.
  revert Γ' v T Ht.
  apply (tm_has_type_mutual_rec
           (fun c v T _ => ∀ Γ, Γ ⊢t u ⋮v U → c = <[z:=U]> Γ → Γ ⊢t {z := u }v v ⋮v T)
           (fun c v T _ => ∀ Γ, Γ ⊢t u ⋮v U → c = <[z:=U]> Γ → Γ ⊢t {z := u }t v ⋮t T));
  (* [context] should be defined as a notation which helps resolving typeclass
  instances for, e.g., rewriting. *)
    unfold context;
    intros; subst; simpl in *; eauto; try econstructor; eauto.
  case_decide; subst.
  by simplify_map_eq.
  econstructor. by simplify_map_eq.

  all:
  instantiate_atom_listctx;
  rewrite <- subst_open_var_tm by
    (eauto using basic_typing_regular_value; my_set_solver);
  auto_eapply; [
      eapply basic_typing_weaken_insert_value; eauto; my_set_solver
    | apply insert_commute; my_set_solver ].
Qed.

Lemma basic_typing_subst_value: forall Γ z u U (v: value) T, Γ ⊢t u ⋮v U -> <[z := U]> Γ ⊢t v ⋮v T -> Γ ⊢t {z := u}v v ⋮v T.
Proof.
  intros * Hu Ht; remember (<[z:=U]> Γ) as Γ'; generalize dependent Γ.
  revert Γ' v T Ht.
  apply (value_has_type_mutual_rec
           (fun c v T _ => ∀ Γ, Γ ⊢t u ⋮v U → c = <[z:=U]> Γ → Γ ⊢t {z := u }v v ⋮v T)
           (fun c v T _ => ∀ Γ, Γ ⊢t u ⋮v U → c = <[z:=U]> Γ → Γ ⊢t {z := u }t v ⋮t T));
    unfold context;
    intros; subst; simpl in *; eauto; try econstructor; eauto.
  case_decide; subst.
  by simplify_map_eq.
  econstructor. by simplify_map_eq.

  all:
  instantiate_atom_listctx;
  rewrite <- subst_open_var_tm by
    (eauto using basic_typing_regular_value; my_set_solver);
  auto_eapply; [
      eapply basic_typing_weaken_insert_value; eauto; my_set_solver
    | apply insert_commute; my_set_solver ].
Qed.

(** * Type uniqueness lemmas *)

Lemma basic_typing_tm_unique: forall Γ e T1 T2,
    Γ ⊢t e ⋮t T1 ->
    Γ ⊢t e ⋮t T2 ->
    T1 = T2.
Proof.
  intros * H. revert T2. revert_all.
  apply (tm_has_type_mutual_rec
           (fun Γ v T1 _ => ∀ T2, Γ ⊢t v ⋮v T2 → T1 = T2)
           (fun Γ e T1 _ => ∀ T2, Γ ⊢t e ⋮t T2 → T1 = T2));
    intros;
    try match goal with
      | H : _ ⊢t _ ⋮t _ |- _ => sinvert H
      | H : _ ⊢t _ ⋮v _ |- _ => sinvert H
      end; eauto;
    try solve [ auto_pose_fv x; repeat specialize_with x; hauto ].
  by simplify_map_eq.
Qed.

Lemma basic_typing_value_unique: forall Γ v T1 T2,
    Γ ⊢t v ⋮v T1 ->
    Γ ⊢t v ⋮v T2 ->
    T1 = T2.
Proof.
  intros * H. revert T2. revert_all.
  apply (value_has_type_mutual_rec
           (fun Γ v T1 _ => ∀ T2, Γ ⊢t v ⋮v T2 → T1 = T2)
           (fun Γ e T1 _ => ∀ T2, Γ ⊢t e ⋮t T2 → T1 = T2));
    intros;
    try match goal with
      | H : _ ⊢t _ ⋮t _ |- _ => sinvert H
      | H : _ ⊢t _ ⋮v _ |- _ => sinvert H
      end; eauto;
    try solve [ auto_pose_fv x; repeat specialize_with x; hauto ].
  by simplify_map_eq.
Qed.

(** * Strengthening lemmas *)

(* This statement can be generalized to taking a union of the context and a
disjoint new context. *)
Lemma basic_typing_strengthen_tm: forall Γ x Tx (v: tm) T,
    (<[x:=Tx]>Γ) ⊢t v ⋮t T -> x # v -> Γ ⊢t v ⋮t T
with basic_typing_strengthen_value: forall Γ x Tx (v: value) T,
    (<[x:=Tx]>Γ) ⊢t v ⋮v T -> x # v -> Γ ⊢t v ⋮v T.
Proof.
  all : intros * H Hfresh; remember (<[x:=Tx]>Γ);
    generalize dependent Γ;
    destruct H; intros; unfold context in *; subst;
    econstructor; eauto;
    try solve [
        try instantiate_atom_listctx;
        try rewrite insert_commute in * by my_set_solver;
        auto_eapply; eauto;
        match goal with
        | |- context [{_ ~t> _} _] =>
            eapply not_elem_of_weaken; [ | apply open_fv_tm ]; my_set_solver
        | _ => my_set_solver
        end ].
  by rewrite lookup_insert_ne in * by my_set_solver.
Qed.

(** * Type properties of syntax sugar *)

Lemma mk_app_has_type Γ e v T1 T2 :
  Γ ⊢t e ⋮t T1 ⤍ T2 ->
  Γ ⊢t v ⋮t T1 ->
  Γ ⊢t mk_app e v ⋮t T2.
Proof.
  intros.
  econstructor; eauto.
  assert (lc e) by basic_typing_regular_simp.
  assert (lc v) by basic_typing_regular_simp.
  instantiate_atom_listctx.
  simpl. rewrite open_rec_lc_tm; eauto.
  econstructor. apply basic_typing_weaken_insert_tm; eauto. my_set_solver.
  auto_exists_L_intros.
  econstructor. instantiate (1 := T2). instantiate (1 := T1).
  econstructor. setoid_rewrite lookup_insert_ne. setoid_rewrite lookup_insert. eauto. my_set_solver.
  econstructor. simplify_map_eq; eauto.
  auto_exists_L_intros.
  econstructor. econstructor. simplify_map_eq; eauto.
Qed.

Lemma mk_app_has_type_inv Γ e v T2 :
  Γ ⊢t mk_app e v ⋮t T2 ->
  lc v ->
  exists T1, Γ ⊢t e ⋮t T1 ⤍ T2 /\ Γ ⊢t v ⋮t T1.
Proof.
  intros.
  sinvert H. simpl in H6. auto_pose_fv x. repeat specialize_with x.
  rewrite open_rec_lc_tm in H6 by auto.
  sinvert H6. remember x. auto_pose_fv y. repeat specialize_with y.
  sinvert H8. simpl in H12. auto_pose_fv z. repeat specialize_with z. subst.
  sinvert H12. sinvert H7. setoid_rewrite lookup_insert in H8. simplify_eq.
  sinvert H11. setoid_rewrite lookup_insert in H7. simplify_eq.
  sinvert H9. setoid_rewrite lookup_insert_ne in H7. setoid_rewrite lookup_insert in H7. simplify_eq.
  exists T3. intuition; eauto.
  eapply basic_typing_strengthen_tm; eauto.
  all: my_set_solver.
Qed.

(** * Type preservation *)

Lemma tr_reduction_sound op c1 c :
  ⇓{op~c1}{c} ->
  ty_of_const c = ret_ty_of_op op.
Proof.
  induction 1; eauto.
Qed.

(** perservation *)
Lemma preservation: forall Γ T (e e': tm), e ↪ e' -> Γ ⊢t e ⋮t T -> Γ ⊢t e' ⋮t T.
Proof.
  intros * Hstep HT. move HT at top. revert_all.
  induction 1; intros; sinvert Hstep; eauto.
  - auto_pose_fv x. repeat specialize_with x.
    rewrite <- subst_intro_tm with (x:=x) by (eauto; my_set_solver).
    sinvert HT. eauto using basic_typing_subst_tm.
  - auto_pose_fv x. repeat specialize_with x.
    rewrite <- subst_intro_tm with (x:=x) by (eauto; my_set_solver).
    sinvert H0. erewrite <- tr_reduction_sound in H1; eauto.
    eauto using basic_typing_subst_tm.
  - sinvert H.
    econstructor; eauto.
    auto_pose_fv x. repeat specialize_with x.
    rewrite <- subst_intro_tm with (x:=x) by (eauto; my_set_solver).
    eauto using basic_typing_subst_tm.
  - sinvert H.
    econstructor; eauto.
    auto_pose_fv x. repeat specialize_with x.
    rewrite <- subst_intro_value with (x:=x) by (eauto; my_set_solver).
    eauto using basic_typing_subst_value.
Qed.

(** multi preservation *)
Lemma multi_preservation: forall Γ T (e e': tm),e ↪* e' -> Γ ⊢t e ⋮t T -> Γ ⊢t e' ⋮t T.
Proof.
  induction 1; eauto using preservation.
Qed.

Ltac pure_multistep_tac :=
  match goal with
  | [H: _ |- (treturn ?v) ↪* (treturn _) ] =>
      apply (value_reduction_any_ctx v); eauto
  end; basic_typing_regular_simp.

Ltac unique_basic_type :=
  repeat match goal with
    | [ H: ?g ⊢t ?e ⋮t ?T, H': ?g ⊢t ?e ⋮t ?T |- _ ] => clear H'
    | [ H: ?g ⊢t ?e ⋮v ?T, H': ?g ⊢t ?e ⋮v ?T |- _ ] => clear H'
    | [ H: ?g ⊢t ?e ⋮t ?T, H': ?g ⊢t ?e ⋮t ?T' |- _ ] =>
        assert (T = T') by eauto using basic_typing_tm_unique; subst
    | [ H: ?g ⊢t ?e ⋮v ?T, H': ?g ⊢t ?e ⋮v ?T' |- _ ] =>
        assert (T = T') by eauto using basic_typing_value_unique; subst
    end.

Ltac basic_typing_solver :=
  match goal with
  | [H: ?g ⊢t (treturn ?v) ⋮t ?t |- ?g ⊢t ?v ⋮v ?t ] => sinvert H; eauto
  end.
